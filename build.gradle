plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.5'
    id 'io.spring.dependency-management' version '1.1.6'
}

allprojects {
    group = 'net.loove'
    version = '0.0.1-SNAPSHOT'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    repositories {
        mavenCentral()
        mavenLocal()
    }
}

ext {
    set('springCloudVersion', "2023.0.3")
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    dependencies {
        implementation 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        runtimeOnly 'org.springframework.boot:spring-boot-docker-compose'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

        if (project.name != 'shared') {
            implementation project(':shared')
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    tasks.register('containerize') {
        doLast {
            println "Building docker image for ${project.name}"

            exec {
                commandLine 'docker', 'buildx', 'build', '-t', "loove/${project.name}:latest", "--build-arg",
                        "MODULE_NAME=${project.name}", "."
            }
        }
    }
}

tasks.register('containerizeAll') {
    doLast {
        for (final def subproject in subprojects) {
            if (subproject.name == 'shared') {
                continue;
            }

            println "Building docker image for ${subproject.name}"

            exec {
                commandLine 'docker', 'buildx', 'build', '-t', "loove/${subproject.name}:latest", "--build-arg",
                        "MODULE_NAME=${subproject.name}", "."
            }

        }
    }

}